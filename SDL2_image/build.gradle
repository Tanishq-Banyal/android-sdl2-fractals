apply plugin: 'com.android.model.native'

def lib_distribution_root = '../distribution'
model {

    repositories {
        libs(PrebuiltLibraries) {
            SDL2_jpeg {
                headers.srcDir "../SDL2_jpeg/include"
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("${lib_distribution_root}/SDL2_jpeg/lib/${targetPlatform.getName()}/libSDL2_jpeg.so")
                }
            }
            SDL2_png {
                headers.srcDir "../SDL2_png/include"
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("${lib_distribution_root}/SDL2_png/lib/${targetPlatform.getName()}/libSDL2_png.so")
                }
            }
            SDL2 {
                headers.srcDir "../SDL2/include"
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("${lib_distribution_root}/SDL2/lib/${targetPlatform.getName()}/libSDL2.so")
                }
            }
        }
    }

    android {
        compileSdkVersion = gradle.sdkVersion
        buildToolsVersion = '25.0.3'

        defaultConfig {
            minSdkVersion.apiLevel = 13
            versionCode = 1
            versionName = '1.0'
        }
        ndk {
            moduleName = 'SDL2_image'
            ldLibs.addAll(["GLESv1_CM", "EGL", "GLESv2", "log", "android", "dl"])
            CFlags.addAll(["-DGL_GLEXT_PROTOTYPES"])
            CFlags.addAll(["-I" + file("include/").absolutePath,
                           "-DGL_GLEXT_PROTOTYPES",
                           "-DLOAD_JPG",
                           "-DLOAD_PNG",
                           "-DLOAD_XPM"
            ])
        }

        sources {
            main {
                jni {
                    dependencies {
                        library 'SDL2' linkage 'shared'
                        library 'SDL2_jpeg' linkage 'shared'
                        library 'SDL2_png' linkage 'shared'
                    }
                    source {
                        srcDir "src"

                    }
                }
            }
        }

    }
}

task distributeLib(type : Copy, dependsOn: [':SDL2_jpeg:distributeLib', ':SDL2_png:distributeLib']) {
    // trigger build library
    dependsOn assemble
    into '../distribution/SDL2_image/'
    from('build/outputs/native/release/lib') {
        into 'lib/'
    }
}
